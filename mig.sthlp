{smcl}
{* *! version 1.0.0  24sep2021}{...}
{hline}
{cmd:help mig} {right:also see:  {help mie}}
{hline}

{title:Title}

{phang}
{bf:mig} {hline 2} Combines estimates over multiple imputations.


{marker syntax}{...}
{title:Syntax}

{p 4 6 2}
{cmdab:mig} {cmd:,} {opth testsyntax(string)} 
	{opth pe(string)}* {opth ve(string)}* 
	{opth chi2(string)}* {opth fvalue(string)}* {opth pvalue(string)}*
	[{it:options}]

{synoptset 35 tabbed}{...}
{synopthdr}
{synoptline}
{syntab:Command*}
{synopt:{opth testsyntax(string)}}estimation command to be applied to each imputation set{p_end}

{syntab:Output to be combined*}
{synopt:{opth pe(string)}}point estimate returned by {cmd:testsyntax}{p_end}
{synopt:{opth ve(string)}}variance estimate returned by {cmd:testsyntax}{p_end}
{synopt:{opth chi(string)}}chi-squared statistic returned by {cmd:testsyntax}{p_end}
{synopt:{opth fvalue(string)}}F-statistic returned by {cmd:testsyntax}{p_end}
{synopt:{opth pvalue(string)}}p-value returned by {cmd:testsyntax}{p_end}

{syntab:Options}
{synopt:{opth dfm:ethod(string)}}method for the degrees of freedom, either {it:large} (default) or {it:small}{p_end}
{synopt:{opth dfr:esidual(string)}}residual degrees of freedom, either a number or an object returned by {cmd:testsyntax}{p_end}
{synopt:{opth dfp:parameter(numlist)}}number of parameter to calculate the residual degrees of freedom, single number{p_end}
{synopt:{opth chi2_df(string)}}degrees of freedom for the chi-squared statistic, single number or object returned by {cmd:testsyntax}{p_end}
{synopt:{opth f_df(string)}}degrees of freedom for numerator of the F-statistic, single number or object returned by {cmd:testsyntax}{p_end}
{synopt:{opth nimp(numlist)}}number of imputations, usually derived from data{p_end}
{synopt:{opth converged(string)}}specifies whether the command converged, an object returned by {cmd:testsyntax}{p_end}
{synoptline}
{p2colreset}{...}
{p 4 6 2}* {cmd:testsyntax} and one of {cmd:pe} and {cmd:ve}, {cmd:chi2}, {cmd:fvalue} or {cmd:pvalue} is required.{p_end}


{marker description}{...}
{title:Description}

{pstd}
{cmd:mig} combines estimates over multiple imputations
	using a point estimate ({cmd:pe}) and a variance estimate ({cmd:ve}) 
		combined via Rubin's rule ({help mig##Rubin2004:Rubin (2004)}),
	a chi-squared statistic ({cmd:chi2}) with ({cmd:chi2_df}) degrees of freedom or 
	an F-statistic ({cmd:fvalue}) with ({cmd:f_df}) degrees of freedom combined via the D2 procedure
	({help mig##Li1991:Li (1991)}),
	or a p-value ({cmd:pvalue}) combined via the median P rule ({help mig##Eekhout2017:Eekhout (2017)}).

{pstd}	
	Required inputs are either a data set that has been generated by {manhelp mi_impute MI:mi impute} 
	in the {it:flong} format
	and an estimation command defined in {cmd:testsyntax}, or
	a summary data set with one row per imputation (and {cmd:testsyntax(}{it:none}{cmd:)}).
	One of {cmd:pe} and {cmd:ve}, {cmd:chi2}, {cmd:fvalue} or {cmd:pvalue} has to be defined
	and refer to an object returned by {cmd:testsyntax} or a variable in the summary data set, repsectively.
	

{marker options}{...}
{title:Options}
{p 4 6 2}

{dlgtab:Main}

{phang}
{opth testsyntax(string)} specifies the command applied to each imputation set.
	Should return the objects referred to in {cmd:pe}, {cmd:ve}, {cmd:chi2}, {cmd:fvalue} or {cmd:pvalue}.
	If set to {it:none}, the data set is assumed to be a summary with one row per imputation and 
	{cmd:pe}, {cmd:ve}, {cmd:chi2}, {cmd:fvalue} or {cmd:pvalue} should refer to variables.

{phang}
{opth pe(string)} specifies where the point estimate is stored, 
	should refer to an object returned by {cmd:testsyntax} or a variable name (if {cmd:testsyntax(}{it:none}{cmd:)}).
	If both {cmd:pe} and {cmd:ve} are given,
	point and variance estimates are combined using  Rubin's rule ({help mig##Rubin2004:Rubin (2004)}) 
	

{phang}
{opth ve(string)} specifies where the variance estimate is stored, 
	should refer to an object returned by {cmd:testsyntax} or a variable name (if {cmd:testsyntax(}{it:none}{cmd:)}).
	If both {cmd:pe} and {cmd:ve} are given,
	point and variance estimates are combined using  Rubin's rule ({help mig##Rubin2004:Rubin (2004)}).
	
{phang}
{opth chi2(string)} specifies where the chi-squared statistic is stored, 
	should refer to an object returned by {cmd:testsyntax} or a variable name (if {cmd:testsyntax(}{it:none}{cmd:)}).
	Chi-squared statistics are combined using the D2 procedure ({help mig##Li1991:Li (1991)}). 
	This method of pooling is less well-established than Rubin's rule.
	
{phang}
{opth fvalue(string)}specifies where the F-statistic is stored, 
	should refer to an object returned by {cmd:testsyntax} or a variable name (if {cmd:testsyntax(}{it:none}{cmd:)}).
	F-statistics are combined via their relation to chi-squared statistics 
	using the D2 procedure ({help mig##Li1991:Li (1991)}).
	This method of pooling is less well-established than Rubin's rule.

{phang}
{opth pvalue(string)} specifies where the p-value is stored, 
	should refer to an object returned by {cmd:testsyntax} or a variable name (if {cmd:testsyntax(}{it:none}{cmd:)}).
	P-values are combined using the median P rule ({help mig##Eekhout2017:Eekhout (2017)}).
	This method of pooling is currently not well-established and should only be used as comparison.

{dlgtab:Options}

{phang}
{opth dfm:ethod(string)} specifies whether the 
	large sample approximation {it:large} (the default) or 
	the small sample correction of {help mie##BR1999:Barnard and Rubin (1999)}
	should be used to calculate the degrees of freedom. 
	For {it:small} the residual degrees of freedom or the number of parameters have to be specified 
	using {cmd:dfresidual} or {cmd:dfpparameter}, repsectively.
	
{phang}
{opth dfr:esidual(string)} specifies the residual degrees of freedom. 
	Can be either a number or an object returned by {cmd:testsyntax}. Does not have an effect of 
	{cmd:dfmethod} is {it:large}.

{phang}
{opth dfp:parameter(numlist)} single number that specifies the number of parameter to calculate 
	the residual degrees of freedom if {cmd:dfmethod} is {it:small} and {cmd:dfresidual} is not given.
	Does not have an effect if {cmd:dfmethod} is {it:large} or {cmd:dfresidual} is defined.

{phang}
{opth chi2_df(string)} specifies the degrees of freedom for the chi-squared statistic. 
	Can be a single number or object returned by {cmd:testsyntax}. 
	If not given a default of 1 is assumed.
	Does not have an effect if {cmd:chi2} is not defined. 

{phang}
{opth f_df(string)} specifies the degrees of freedom for the numerator of the F-statistic. 
	Can be a single number or object returned by {cmd:testsyntax}.
	If not given a default of 1 is assumed.
	Does not have an effect if {cmd:fvalue} is not defined.

{phang}
{opth nimp(numlist)} specifies the number of imputations. By default this 
	is derived from the data. If it is smaller than the available number of imputations 
	the first {it:nimp} number of imputations are used. Cannot be larger then the number of imputations 
	in the data.

{phang}
{opth converged(string)} specifies whether the command converged, an object returned by {cmd:testsyntax}


{marker results}{...}
{title:Stored results}

{synoptset 22 tabbed}{...}
{pstd}If {cmd:pe} and {cmd:ve} are given:{p_end}
{synopt:{cmd:r(M)}}scalar with number of imputations{p_end}
{synopt:{cmd:r(table)}}matrix containing the pooled estimate with standard error, test statistic, p-value, and confidence interval.

{pstd}If {cmd:chi2} or {cmd:fvalue} are given:{p_end}
{synopt:{cmd:r(M)}}scalar with number of imputations{p_end}
{synopt:{cmd:r(av_chi2)}}scalar with average chi-squared value (with an F-distribution){p_end}
{synopt:{cmd:r(r(chi2_df)}}scalar with initial degrees of freedom for chi-squared test{p_end}
{synopt:{cmd:r(vx)}}scalar with numerator degrees of freedom of the resulting F-statistic{p_end}
{synopt:{cmd:r(Dx)}}scalar with denominator degrees of freedom of the resulting F-statistic{p_end}
{synopt:{cmd:r(p_chi2)}}scalar with p-value from the resulting F-test{p_end}

{pstd}If {cmd:pvalue} is given:{p_end}
{synopt:{cmd:r(M)}}scalar with number of imputations{p_end}
{synopt:{cmd:r(p_med)}}median p-value{p_end}


{marker examples}{...}
{title:Examples}

{pstd}Data{p_end}
{phang2}{cmd:. webuse mheart1s20}{p_end}
{phang2}{cmd:. mi convert flong}{p_end}

{pstd}Linear regression (as with mi estimate: regress){p_end}
{phang2}{cmd:. mig, testsyntax(regress bmi attack) pe(r(table)[1,1]) ve(r(table)[2,1]^2) dfm(small) dfr(e(df_r))}{p_end}

{pstd}Risk difference{p_end}
{phang2}{cmd:. mig, testsyntax(cs attack female) pe(r(rd)) ve(((r(ub_rd)-r(rd))/invnormal(0.975))^2)}{p_end}

{pstd}Risk ratio{p_end}
{phang2}{cmd:. mig, testsyntax(cs attack female) pe(log(r(rr))) ve(((log(r(ub_rr))-log(r(lb_rr)))/2/invnormal(0.975))^2)}{p_end}

{pstd}Chi-squared statistic{p_end}
{phang2}{cmd:. mig, testsyntax(tab attack smokes, chi2) chi2(r(chi2)) chi2_df(1)}{p_end}

{pstd}Median p-value{p_end}
{phang2}{cmd:. mig, testsyntax(tab attack hsgrad, chi2) pvalue(r(p))}


{marker references}{...}
{title:References}

{marker BR1999}{...}
{phang}
Barnard, J., and Rubin, D.B. 1999.  
	Small-sample degrees of freedom with multiple imputation. 
	{it:Biometrika} 86: 948-955.

{marker Eekhout2017}{...}
{phang}
Eekhout, I., Van De Wiel, M. A., & Heymans, M. W. 2017. 
	Methods for significance testing of categorical covariates 
	in logistic regression models after multiple imputation: 
	power and applicability analysis. 
	{it:BMC medical research methodology} 17(1), 1-12.	
	
{marker Li1991}{...}
{phang}
Li KH, Meng XL, Raghunathan TE, Rubin DB. 1991. 
	Significance levels from repeated p-values with multiply-imputed data. 
	{it:Statistica Sinica} 1(1):65-92.
	
{marker Rubin2004}{...}
{phang}
Rubin, D.B. 2004. Multiple Imputation for Nonresponse in Surveys. New York: John Wiley and Sons. 




	
	
